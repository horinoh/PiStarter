'CONST #X=0:CONST #Y=1:CONST #Z=2:CONST #W=3
'CONST #EPSILON=0.0001
VAR _X=0,_Y=1,_Z=2,_W=3
VAR _EPSILON=0.0001

COMMON DEF FRAC(X) RETURN X-FLOOR(X) END

COMMON DEF VERIFY(L[],R[])
 IF LEN(L)!=LEN(R) THEN RETURN FALSE
 VAR I%
 FOR I%=0 TO LEN(L)-1
  IF ABS(L[I%]-R[I%])>0.01 THEN RETURN FALSE
 NEXT
 RETURN TRUE
END

COMMON DEF DOT(L[],R[])
 VAR O[LEN(L)]
 VAR I%
 'ARYOP #AOPMUL,O,L,R
 FOR I%=0 TO LEN(O)-1 O[I%]=L[I%]*R[I%] NEXT
 VAR SUM=0
 FOR I%=0 TO LEN(O)-1
  SUM=SUM+O[I%]
 NEXT
 RETURN SUM
END

COMMON DEF NORMALIZE O[],V[]
 VAR L=DOT(V,V)
 'IF L>#EPSILON THEN
 IF L>_EPSILON THEN
  'ARYOP #AOPDIV,O,V,SQR(L)
  L=SQR(L):VAR I%:FOR I%=0 TO LEN(O)-1 O[I%]=V[I%]/L NEXT
 ENDIF
END

COMMON DEF CROSS O[],L[],R[]
 'O[#X]=L[#Y]*R[#Z]-L[#Z]*R[#Y]
 'O[#Y]=L[#Z]*R[#X]-L[#X]*R[#Z]
 'O[#Z]=L[#X]*R[#Y]-L[#Y]*R[#X]
 O[_X]=L[_Y]*R[_Z]-L[_Z]*R[_Y]
 O[_Y]=L[_Z]*R[_X]-L[_X]*R[_Z]
 O[_Z]=L[_X]*R[_Y]-L[_Y]*R[_X]
END

' DETERMINANT
COMMON DEF DETERMINANT2(M[])
 RETURN M[0,0]*M[1,1]-M[1,0]*M[0,1]
END

COMMON DEF DETERMINANT3(M[])
 'VAR M2[2,2]=[M[1,1],M[1,2], M[2,1],M[2,2]]
 VAR M2[2,2]
 M2[0,0]=M[1,1]:M2[0,1]=M[1,2]:M2[1,0]=M[2,1]:M2[1,1]=M[2,2]
 VAR A=DETERMINANT2(M2)
 M2[0,0]=M[0,1]
 M2[0,1]=M[0,2]
 VAR B=DETERMINANT2(M2)
 M2[1,0]=M[1,1]
 M2[1,1]=M[1,2]
 VAR C=DETERMINANT2(M2)
 RETURN M[0,0]*A-M[1,0]*B+M[2,0]*C
END

COMMON DEF DETERMINANT4(M[])
 'VAR M3[3,3]=[M[1,1],M[1,2],M[1,3], M[2,1],M[2,2],M[2,3], M[3,1],M[3,2],M[3,3]]
 VAR M3[3,3]
 M3[0,0]=M[1,1]:M3[0,1]=M[1,2]:M3[0,2]=M[1,3]
 M3[1,0]=M[2,1]:M3[1,1]=M[2,2]:M3[1,2]=M[2,3]
 M3[2,0]=M[3,1]:M3[2,1]=M[3,2]:M3[2,2]=M[3,3]
 VAR A=DETERMINANT3(M3)
 M3[0,0]=M[0,1]
 M3[0,1]=M[0,2]
 M3[0,2]=M[0,3]
 VAR B=DETERMINANT3(M3)
 M3[1,0]=M[1,1]
 M3[1,1]=M[1,2]
 M3[1,2]=M[1,3]
 VAR C=DETERMINANT3(M3)
 M3[2,0]=M[2,1]
 M3[2,1]=M[2,2]
 M3[2,2]=M[2,3]
 VAR D=DETERMINANT3(M3)
 RETURN M[0,0]*A-M[1,0]*B+M[2,0]*C-M[3,0]*D
END

' INVERSE
COMMON DEF INVERSE2 O[],M[],DET
 O[0,0]= M[1,1]:O[0,1]=-M[0,1]
 O[1,0]=-M[1,0]:O[1,1]= M[0,0]
 'ARYOP #AOPDIV,O,O,DET
 O[0,0]=O[0,0]/DET:O[0,1]=O[0,1]/DET:O[1,0]=O[1,0]/DET:O[1,1]=O[1,1]/DET
END

COMMON DEF INVERSE3 O[],M[],DET
 O[0,0]=  M[1,1]*M[2,2]-M[2,1]*M[1,2]
 O[0,1]=-(M[0,1]*M[2,2]-M[2,1]*M[0,2])
 O[0,2]=  M[0,1]*M[1,2]-M[1,1]*M[0,2]
 O[1,0]=-(M[1,0]*M[2,2]-M[2,0]*M[1,2])
 O[1,1]=  M[0,0]*M[2,2]-M[2,0]*M[0,2]
 O[1,2]=-(M[0,0]*M[1,2]-M[1,0]*M[0,2])
 O[2,0]=  M[1,0]*M[2,1]-M[2,0]*M[1,1]
 O[2,1]=-(M[0,0]*M[2,1]-M[2,0]*M[0,1])
 O[2,2]=  M[0,0]*M[1,1]-M[1,0]*M[0,1]
 'ARYOP #AOPDIV,O,O,DET
 O[0,0]=O[0,0]/DET:O[0,1]=O[0,1]/DET:O[0,2]=O[0,2]/DET
 O[1,0]=O[1,0]/DET:O[1,1]=O[1,1]/DET:O[1,2]=O[1,2]/DET
 O[2,0]=O[2,0]/DET:O[2,1]=O[2,1]/DET:O[2,2]=O[2,2]/DET
END

COMMON DEF INVERSE4 O[],M[],DET
 VAR M3[3,3]'=[M[1,1],M[1,2],M[1,3], M[2,1],M[2,2],M[2,3], M[3,1],M[3,2],M[3,3]]
 M3[0,0]=M[1,1]:M3[0,1]=M[1,2]:M3[0,2]=M[1,3]
 M3[1,0]=M[2,1]:M3[1,1]=M[2,2]:M3[1,2]=M[2,3]
 M3[2,0]=M[3,1]:M3[2,1]=M[3,2]:M3[2,2]=M[3,3]
 O[0,0]=DETERMINANT3(M3)
 M3[0,0]=M[0,1]
 M3[0,1]=M[0,2]
 M3[0,2]=M[0,3]
 O[0,1]=-DETERMINANT3(M3)
 M3[1,0]=M[1,1]
 M3[1,1]=M[1,2]
 M3[1,2]=M[1,3]
 O[0,2]= DETERMINANT3(M3)
 M3[2,0]=M[2,1]
 M3[2,1]=M[2,2]
 M3[2,2]=M[2,3]
 O[0,3]=-DETERMINANT3(M3)
 M3[0,0]=M[1,0]:M3[0,1]=M[1,2]:M3[0,2]=M[1,3]
 M3[1,0]=M[2,0]:M3[1,1]=M[2,2]:M3[1,2]=M[2,3]
 M3[2,0]=M[3,0]:M3[2,1]=M[3,2]:M3[2,2]=M[3,3]
 O[1,0]=-DETERMINANT3(M3)
 M3[0,0]=M[0,0]
 M3[0,1]=M[0,2]
 M3[0,2]=M[0,3]
 O[1,1]= DETERMINANT3(M3)
 M3[1,0]=M[1,0]
 M3[1,1]=M[1,2]
 M3[1,2]=M[1,3]
 O[1,2]=-DETERMINANT3(M3)
 M3[2,0]=M[2,0]
 M3[2,1]=M[2,2]
 M3[2,2]=M[2,3]
 O[1,3]= DETERMINANT3(M3)
 M3[0,0]=M[1,0]:M3[0,1]=M[1,1]:M3[0,2]=M[1,3]
 M3[1,0]=M[2,0]:M3[1,1]=M[2,1]:M3[1,2]=M[2,3]
 M3[2,0]=M[3,0]:M3[2,1]=M[3,1]:M3[2,2]=M[3,3]
 O[2,0]= DETERMINANT3(M3)
 M3[0,0]=M[0,0]
 M3[0,1]=M[0,1]
 M3[0,2]=M[0,3]
 O[2,1]=-DETERMINANT3(M3)
 M3[1,0]=M[1,0]
 M3[1,1]=M[1,1]
 M3[1,2]=M[1,3]
 O[2,2]= DETERMINANT3(M3)
 M3[2,0]=M[2,0]
 M3[2,1]=M[2,1]
 M3[2,2]=M[2,3]
 O[2,3]=-DETERMINANT3(M3)
 M3[0,0]=M[1,0]:M3[0,1]=M[1,1]:M3[0,2]=M[1,2]
 M3[1,0]=M[2,0]:M3[1,1]=M[2,1]:M3[1,2]=M[2,2]
 M3[2,0]=M[3,0]:M3[2,1]=M[3,1]:M3[2,2]=M[3,2]
 O[3,0]=-DETERMINANT3(M3)
 M3[0,0]=M[0,0]
 M3[0,1]=M[0,1]
 M3[0,2]=M[0,2]
 O[3,1]= DETERMINANT3(M3)
 M3[1,0]=M[1,0]
 M3[1,1]=M[1,1]
 M3[1,2]=M[1,2]
 O[3,2]=-DETERMINANT3(M3)
 M3[2,0]=M[2,0]
 M3[2,1]=M[2,1]
 M3[2,2]=M[2,2]
 O[3,3]= DETERMINANT3(M3)
 'ARYOP #AOPDIV,O,O,DET
 O[0,0]=O[0,0]/DET:O[0,1]=O[0,1]/DET:O[0,2]=O[0,2]/DET:O[0,3]=O[0,3]/DET
 O[1,0]=O[1,0]/DET:O[1,1]=O[1,1]/DET:O[1,2]=O[1,2]/DET:O[1,3]=O[1,3]/DET
 O[2,0]=O[2,0]/DET:O[2,1]=O[2,1]/DET:O[2,2]=O[2,2]/DET:O[2,3]=O[2,3]/DET
 O[3,0]=O[3,0]/DET:O[3,1]=O[3,1]/DET:O[3,2]=O[3,2]/DET:O[3,3]=O[3,3]/DET
END

COMMON DEF INVERSEQ O[],Q[]
 CONJUGATE O,Q
 'ARYOP #AOPDIV O,O,DOT(Q,Q)
 VAR L=DOT(Q,Q):VAR I%:FOR I%=0 TO LEN(O)-1 O[I%]=O[I%]/L NEXT
END

' MULTIPLY
COMMON DEF MULTIPLY2 O[],L[],R[]
 VAR AL[8],AR[8],AO[8]
 COPY AL,0,L,0,2
 COPY AL,2,L,0,2
 COPY AL,4,L,2,2
 COPY AL,6,L,2,2
 AR[0]=R[0,0]
 AR[1]=R[1,0]
 AR[2]=R[0,1]
 AR[3]=R[1,1]
 COPY AR,4,AR,0,4
 'ARYOP #AOPMUL,AO,AL,AR
 VAR I%:FOR I%=0 TO LEN(AO)-1 AO[I%]=AL[I%]*AR[I%] NEXT
 O[0,0]=AO[0]+AO[1]
 O[0,1]=AO[2]+AO[3]
 O[1,0]=AO[4]+AO[5]
 O[1,1]=AO[6]+AO[7]
END

COMMON DEF MULTIPLY3 O[],L[],R[]
 VAR AL[27],AR[27],AO[27]
 COPY AL, 0,L,0,3
 COPY AL, 3,L,0,3
 COPY AL, 6,L,0,3
 COPY AL, 9,L,3,3
 COPY AL,12,L,3,3
 COPY AL,15,L,3,3
 COPY AL,18,L,6,3
 COPY AL,21,L,6,3
 COPY AL,24,L,6,3
 AR[0]=R[0,0]
 AR[1]=R[1,0]
 AR[2]=R[2,0]
 AR[3]=R[0,1]
 AR[4]=R[1,1]
 AR[5]=R[2,1]
 AR[6]=R[0,2]
 AR[7]=R[1,2]
 AR[8]=R[2,2]
 COPY AR, 9,AR,0,9
 COPY AR,18,AR,0,9
 'ARYOP #AOPMUL,AO,AL,AR
 VAR I%:FOR I%=0 TO LEN(AO)-1 AO[I%]=AL[I%]*AR[I%] NEXT
 O[0,0]=AO[ 0]+AO[ 1]+AO[ 2]
 O[0,1]=AO[ 3]+AO[ 4]+AO[ 5]
 O[0,2]=AO[ 6]+AO[ 7]+AO[ 8]
 O[1,0]=AO[ 9]+AO[10]+AO[11]
 O[1,1]=AO[12]+AO[13]+AO[14]
 O[1,2]=AO[15]+AO[16]+AO[17]
 O[2,0]=AO[18]+AO[19]+AO[20]
 O[2,1]=AO[21]+AO[22]+AO[23]
 O[2,2]=AO[24]+AO[25]+AO[26]
END

COMMON DEF MULTIPLY4 O[],L[],R[]
 VAR AL[64],AR[64],AO[64]
 COPY AL, 0,L, 0,4
 COPY AL, 4,L, 0,4
 COPY AL, 8,L, 0,4
 COPY AL,12,L, 0,4
 COPY AL,16,L, 4,4
 COPY AL,20,L, 4,4
 COPY AL,24,L, 4,4
 COPY AL,28,L, 4,4
 COPY AL,32,L, 8,4
 COPY AL,36,L, 8,4
 COPY AL,40,L, 8,4
 COPY AL,44,L, 8,4
 COPY AL,48,L,12,4
 COPY AL,52,L,12,4
 COPY AL,56,L,12,4
 COPY AL,60,L,12,4
 AR[ 0]=R[0,0]
 AR[ 1]=R[1,0]
 AR[ 2]=R[2,0]
 AR[ 3]=R[3,0]
 AR[ 4]=R[0,1]
 AR[ 5]=R[1,1]
 AR[ 6]=R[2,1]
 AR[ 7]=R[3,1]
 AR[ 8]=R[0,2]
 AR[ 9]=R[1,2]
 AR[10]=R[2,2]
 AR[11]=R[3,2]
 AR[12]=R[0,3]
 AR[13]=R[1,3]
 AR[14]=R[2,3]
 AR[15]=R[3,3]
 COPY AR,16,AR,0,16
 COPY AR,32,AR,0,16
 COPY AR,48,AR,0,16
 'ARYOP #AOPMUL,AO,AL,AR
 VAR I%:FOR I%=0 TO LEN(AO)-1 AO[I%]=AL[I%]*AR[I%] NEXT
 O[0,0]=AO[ 0]+AO[ 1]+AO[ 2]+AO[ 3]
 O[0,1]=AO[ 4]+AO[ 5]+AO[ 6]+AO[ 7]
 O[0,2]=AO[ 8]+AO[ 9]+AO[10]+AO[11]
 O[0,3]=AO[12]+AO[13]+AO[14]+AO[15]
 O[1,0]=AO[16]+AO[17]+AO[18]+AO[19]
 O[1,1]=AO[20]+AO[21]+AO[22]+AO[23]
 O[1,2]=AO[24]+AO[25]+AO[26]+AO[27]
 O[1,3]=AO[28]+AO[29]+AO[30]+AO[31]
 O[2,0]=AO[32]+AO[33]+AO[34]+AO[35]
 O[2,1]=AO[36]+AO[37]+AO[38]+AO[39]
 O[2,2]=AO[40]+AO[41]+AO[42]+AO[43]
 O[2,3]=AO[44]+AO[45]+AO[46]+AO[47]
 O[3,0]=AO[48]+AO[49]+AO[50]+AO[51]
 O[3,1]=AO[52]+AO[53]+AO[54]+AO[55]
 O[3,2]=AO[56]+AO[57]+AO[58]+AO[59]
 O[3,3]=AO[60]+AO[61]+AO[62]+AO[63]
END

COMMON DEF MULTIPLYQ O[],L[],R[]
 'O[#X]=R[#W]*L[#X]+R[#X]*L[#W]+R[#Y]*L[#Z]-R[#Z]*L[#Y]
 'O[#Y]=R[#W]*L[#Y]-R[#X]*L[#Z]+R[#Y]*L[#W]+R[#Z]*L[#X]
 'O[#Z]=R[#W]*L[#Z]+R[#X]*L[#Y]-R[#Y]*L[#X]+R[#Z]*L[#W]
 'O[#W]=R[#W]*L[#W]-R[#X]*L[#X]-R[#Y]*L[#Y]-R[#Z]*L[#Z]
 O[_X]=R[_W]*L[_X]+R[_X]*L[_W]+R[_Y]*L[_Z]-R[_Z]*L[_Y]
 O[_Y]=R[_W]*L[_Y]-R[_X]*L[_Z]+R[_Y]*L[_W]+R[_Z]*L[_X]
 O[_Z]=R[_W]*L[_Z]+R[_X]*L[_Y]-R[_Y]*L[_X]+R[_Z]*L[_W]
 O[_W]=R[_W]*L[_W]-R[_X]*L[_X]-R[_Y]*L[_Y]-R[_Z]*L[_Z]
END

' TRANSFORM
COMMON DEF TRANSFORM2 O[],V[],M[]
 VAR AL[4],AR[4],AO[4]
 COPY AL,0,V,0,2
 COPY AL,2,V,0,2
 AR[0]=M[0,0]
 AR[1]=M[1,0]
 AR[2]=M[0,1]
 AR[3]=M[1,1]
 'ARYOP #AOPMUL,AO,AL,AR
 VAR I%:FOR I%=0 TO LEN(AO)-1 AO[I%]=AL[I%]*AR[I%] NEXT
 O[0]=AO[0]+AO[1]
 O[1]=AO[2]+AO[3]
END

COMMON DEF TRANSFORM3 O[],V[],M[]
 VAR AL[9],AR[9],AO[9]
 COPY AL,0,V,0,3
 COPY AL,3,V,0,3
 COPY AL,6,V,0,3
 AR[0]=M[0,0]
 AR[1]=M[1,0]
 AR[2]=M[2,0]
 AR[3]=M[0,1]
 AR[4]=M[1,1]
 AR[5]=M[2,1]
 AR[6]=M[0,2]
 AR[7]=M[1,2]
 AR[8]=M[2,2]
 'ARYOP #AOPMUL,AO,AL,AR
 VAR I%:FOR I%=0 TO LEN(AO)-1 AO[I%]=AL[I%]*AR[I%] NEXT
 O[0]=AO[0]+AO[1]+AO[2]
 O[1]=AO[3]+AO[4]+AO[5]
 O[2]=AO[6]+AO[7]+AO[8]
END

COMMON DEF TRANSFORM4 O[],V[],M[]
 VAR AL[16],AR[16],AO[16]
 COPY AL, 0,V,0,4
 COPY AL, 4,V,0,4
 COPY AL, 8,V,0,4
 COPY AL,12,V,0,4
 AR[ 0]=M[0,0]
 AR[ 1]=M[1,0]
 AR[ 2]=M[2,0]
 AR[ 3]=M[3,0]
 AR[ 4]=M[0,1]
 AR[ 5]=M[1,1]
 AR[ 6]=M[2,1]
 AR[ 7]=M[3,1]
 AR[ 8]=M[0,2]
 AR[ 9]=M[1,2]
 AR[10]=M[2,2]
 AR[11]=M[3,2]
 AR[12]=M[0,3]
 AR[13]=M[1,3]
 AR[14]=M[2,3]
 AR[15]=M[3,3]
 'ARYOP #AOPMUL,AO,AL,AR
 VAR I%:FOR I%=0 TO LEN(AO)-1 AO[I%]=AL[I%]*AR[I%] NEXT
 O[0]=AO[ 0]+AO[ 1]+AO[ 2]+AO[ 3]
 O[1]=AO[ 4]+AO[ 5]+AO[ 6]+AO[ 7]
 O[2]=AO[ 8]+AO[ 9]+AO[10]+AO[11]
 O[3]=AO[12]+AO[13]+AO[14]+AO[15]
END

COMMON DEF TRANSFORMQ O[],V[],Q[]
 VAR C[LEN(Q)]
 CONJUGATE C,Q
 VAR VQ[LEN(Q)]
 COPY VQ,0,V,0,LEN(V)
 'VQ[#W]=0
 VQ[_W]=0
 VAR TMP[LEN(Q)]
 MULTIPLYQ TMP,C,VQ
 MULTIPLYQ VQ,TMP,Q
 COPY O,0,VQ,0,LEN(O)
END

' ROTATE
COMMON DEF ROTATE2 O[],R
 VAR C=COS(R)
 VAR S=SIN(R)
 O[0,0]= C:O[0,1]=S
 O[1,0]=-S:O[1,1]=C
END

COMMON DEF ROTATEX3 O[],R
 VAR C=COS(R)
 VAR S=SIN(R)
 O[0,0]=1:O[0,1]= 0:O[0,2]=0
 O[1,0]=0:O[1,1]= C:O[1,2]=S
 O[2,0]=0:O[2,1]=-S:O[2,2]=C
END

COMMON DEF ROTATEX4 O[],R
 ROTATEX3 O,R
 O[0,3]=0:O[1,3]=0:O[2,3]=0:O[3,0]=0:O[3,1]=0:O[3,2]=0
 O[3,3]=1
END

COMMON DEF ROTATEY3 O[],R
 VAR C=COS(R)
 VAR S=SIN(R)
 O[0,0]=C:O[0,1]=0:O[0,2]=-S
 O[1,0]=0:O[1,1]=1:O[1,2]= 0
 O[2,0]=S:O[2,1]=0:O[2,2]= C
END

COMMON DEF ROTATEY4 O[],R
 ROTATEY3 O,R
 O[0,3]=0:O[1,3]=0:O[2,3]=0:O[3,0]=0:O[3,1]=0:O[3,2]=0
 O[3,3]=1
END

COMMON DEF ROTATEZ3 O[],R
 VAR C=COS(R)
 VAR S=SIN(R)
 O[0,0]= S:O[0,1]=C:O[0,2]=0
 O[1,0]=-C:O[1,1]=S:O[1,2]=0
 O[2,0]= 0:O[2,1]=0:O[2,2]=1
END

COMMON DEF ROTATEZ4 O[],R
 ROTATEZ3 O,R
 O[0,3]=0:O[1,3]=0:O[2,3]=0:O[3,0]=0:O[3,1]=0:O[3,2]=0
 O[3,3]=1
END

' ROTATEAXIS ... AX[] MUST BE NORMALIZED
COMMON DEF ROTATEAXIS3 O[],R,AX[]
 VAR C=COS(R)
 VAR S=SIN(R)
 VAR C1=1-C
 'VAR XY=AX[#X]*AX[#Y]
 'VAR YZ=AX[#Y]*AX[#Z]
 'VAR ZX=AX[#Z]*AX[#X]
 'O[0,0]=C+C1*AX[#X]*AX[#X]
 'O[0,1]=C1*XY+S*AX[#Z]
 'O[0,2]=C1*ZX-S*AX[#Y]
 'O[1,0]=C1*XY-S*AX[#Z]
 'O[1,1]=C1*AX[#Y]*AX[#Y]+C
 'O[1,2]=C1*YZ+S*AX[#X]
 'O[2,0]=C1*ZX+S*AX[#Y]
 'O[2,1]=C1*YZ-S*AX[#X]
 'O[2,2]=C1*AX[#Z]*AX[#Z]+C
 VAR XY=AX[_X]*AX[_Y]
 VAR YZ=AX[_Y]*AX[_Z]
 VAR ZX=AX[_Z]*AX[_X]
 O[0,0]=C+C1*AX[_X]*AX[_X]
 O[0,1]=C1*XY+S*AX[_Z]
 O[0,2]=C1*ZX-S*AX[_Y]
 O[1,0]=C1*XY-S*AX[_Z]
 O[1,1]=C1*AX[_Y]*AX[_Y]+C
 O[1,2]=C1*YZ+S*AX[_X]
 O[2,0]=C1*ZX+S*AX[_Y]
 O[2,1]=C1*YZ-S*AX[_X]
 O[2,2]=C1*AX[_Z]*AX[_Z]+C
END

COMMON DEF ROTATEAXIS4 O[],R,AX[]
 ROTATEAXIS3 O,R,AX
 O[0,3]=0:O[1,3]=0:O[2,3]=0:O[3,0]=0:O[3,1]=0:O[3,2]=0
 O[3,3]=1
END

COMMON DEF ROTATEAXISQ O[],R,AX[]
 VAR HR=R*0.5
 'ARYOP #AOPMUL O,AX,SIN(HR)
 VAR S=SIN(HR):O[_X]=AX[_X]*S:O[_Y]=AX[_Y]*S:O[_Z]=AX[_Z]*S
 'O[#W]=COS(HR)
 O[_W]=COS(HR)
END

' SCALE
COMMON DEF SCALE2 O[],S[]
 'O[0,0]=S[#X]:O[0,1]=0
 'O[1,0]=0:O[1,1]=S[#Y]
 O[0,0]=S[_X]:O[0,1]=0
 O[1,0]=0:O[1,1]=S[_Y]
END

COMMON DEF SCALE3 O[],S[]
 SCALE2 O,S
 O[0,2]=0:O[1,2]=0 
 'O[2,0]=0:O[2,1]=0:O[2,2]=S[#Z]
 O[2,0]=0:O[2,1]=0:O[2,2]=S[_Z]
END

COMMON DEF SCALE4 O[],S[]
 SCALE3 O,S
 O[0,3]=0:O[1,3]=0:O[2,3]=0
 O[3,0]=0:O[3,1]=0:O[3,2]=0:O[3,3]=1
END

'TRANSLATE
COMMON DEF TRANSLATE O[],T[]
 'O[3,0]=T[#X]:O[3,1]=T[#Y]:O[3,2]=T[#Z]
 O[3,0]=T[_X]:O[3,1]=T[_Y]:O[3,2]=T[_Z]
END

COMMON DEF PERSPECTIVE O[],FOV,ASP,N,F
 VAR COT=1/TAN(FOV/2)
 VAR R=F/(N-F)
 O[0,0]= COT/ASP
 O[0,1]= 0
 O[0,2]= 0
 O[0,3]= 0
 O[1,0]= 0
 O[1,1]= COT
 O[1,2]= 0
 O[1,3]= 0
 O[2,0]= 0
 O[2,1]= 0
 O[2,2]= R
 O[2,3]=-1
 O[3,0]= 0
 O[3,1]= 0
 O[3,2]= N*R
 O[3,3]= 0
END

COMMON DEF LOOKAT O[],POS[],TAG[],UP[]
 VAR F[LEN(POS)]
 'ARYOP #AOPSUB,F,TAG,POS
 VAR I%:FOR I%=0 TO LEN(F)-1 F[I%]=TAG[I%]-POS[I%] NEXT
 NORMALIZE F,F
 VAR S[LEN(POS)]
 CROSS S,F,UP
 NORMALIZE S,S
 VAR U[LEN(POS)]
 CROSS U,S,F
 'O[0,0]= S[#X]
 'O[0,1]= U[#X]
 'O[0,2]=-F[#X]
 O[0,0]= S[_X]
 O[0,1]= U[_X]
 O[0,2]=-F[_X]
 O[0,3]= 0
 'O[1,0]= S[#Y]
 'O[1,1]= U[#Y]
 'O[1,2]=-F[#Y]
 O[1,0]= S[_Y]
 O[1,1]= U[_Y]
 O[1,2]=-F[_Y]
 O[1,3]= 0
 'O[2,0]= S[#Z]
 'O[2,1]= U[#Z]
 'O[2,2]=-F[#Z]
 O[2,0]= S[_Z]
 O[2,1]= U[_Z]
 O[2,2]=-F[_Z]
 O[2,3]= 0
 O[3,0]=-DOT(S,POS)
 O[3,1]=-DOT(U,POS)
 O[3,2]= DOT(F,POS)
 O[3,3]= 1
END

COMMON DEF VIEWPORT O[],X,Y,W,H,N,F
 VAR W2=W/2
 VAR H2=H/2
 VAR FN=F-N

 ' GL
 'O[1,1]=H2:O[2,2]=FN/2:O[3,2]=(N+F)/2
 ' DX
 O[1,1]=-H2:O[2,2]=FN:O[3,2]=N

 O[0,0]=W2
 O[0,1]=0
 O[0,2]=0
 O[0,3]=0
 O[1,0]=0
 O[1,2]=0
 O[1,3]=0
 O[2,0]=0
 O[2,1]=0
 O[2,3]=0
 O[3,0]=X+W2
 O[3,1]=Y+H2
 O[3,3]=1
END

COMMON DEF CONJUGATE O[],Q[]
 'O[#X]=-Q[#X]:O[#Y]=-Q[#Y]:O[#Z]=-Q[#Z]:O[#W]=Q[#W]
 O[_X]=-Q[_X]:O[_Y]=-Q[_Y]:O[_Z]=-Q[_Z]:O[_W]=Q[_W]
END

COMMON DEF SLERP O[],L[],R[],T
 VAR AC=DOT(L,R)
 VAR RR[LEN(R)]
 IF AC<0 THEN
  AC=-AC
  'RR[#X]=-R[#X]:RR[#Y]=-R[#Y]:RR[#Z]=-R[#Z]:RR[#W]=-R[#W]
  RR[_X]=-R[_X]:RR[_Y]=-R[_Y]:RR[_Z]=-R[_Z]:RR[_W]=-R[_W]
 ELSE
  COPY RR,0,R,0,LEN(R)
 ENDIF
 'IF AC>1-#EPSILON THEN
 IF AC>1-_EPSILON THEN
  'ARYOP #AOPLIP O,RR,L,T 'R*T+L*(1-T)
  VAR I%:FOR I%=0 TO LEN(O)-1 O[I%]=L[I%]*(1-T)+RR[I%]*T NEXT
 ELSE
  VAR A=ACOS(AC)
  VAR S=SIN(A)
  VAR SL=SIN(A*(1-T))/S
  VAR SR=SIN(A*T)/S
  'O[#X]=L[#X]*SL+RR[#X]*SR
  'O[#Y]=L[#Y]*SL+RR[#Y]*SR
  'O[#Z]=L[#Z]*SL+RR[#Z]*SR
  'O[#W]=L[#W]*SL+RR[#W]*SR
  O[_X]=L[_X]*SL+RR[_X]*SR
  O[_Y]=L[_Y]*SL+RR[_Y]*SR
  O[_Z]=L[_Z]*SL+RR[_Z]*SR
  O[_W]=L[_W]*SL+RR[_W]*SR
 ENDIF
END

COMMON DEF QUAT_FROM_PITCHYAWROLL O[],P,Y,R
 VAR HP=P*0.5
 VAR HY=Y*0.5
 VAR HR=R*0.5
 VAR C[3]'=[COS(HP),COS(HY),COS(HR)]
 C[_X]=COS(HP):C[_Y]=COS(HY):C[_Z]=COS(HR)
 VAR S[3]'=[SIN(HP),SIN(HY),SIN(HR)]
 S[_X]=SIN(HP):S[_Y]=SIN(HY):S[_Z]=SIN(HR)
 'O[#X]=S[#X]*C[#Y]*C[#Z]-C[#X]*S[#Y]*S[#Z]
 'O[#Y]=C[#X]*S[#Y]*C[#Z]+S[#X]*C[#Y]*S[#Z]
 'O[#Z]=C[#X]*C[#Y]*S[#Z]-S[#X]*S[#Y]*C[#Z]
 'O[#W]=C[#X]*C[#Y]*C[#Z]+S[#X]*S[#Y]*S[#Z]
 O[_X]=S[_X]*C[_Y]*C[_Z]-C[_X]*S[_Y]*S[_Z]
 O[_Y]=C[_X]*S[_Y]*C[_Z]+S[_X]*C[_Y]*S[_Z]
 O[_Z]=C[_X]*C[_Y]*S[_Z]-S[_X]*S[_Y]*C[_Z]
 O[_W]=C[_X]*C[_Y]*C[_Z]+S[_X]*S[_Y]*S[_Z]
END

COMMON DEF QUAT_FROM_TWOVEC O[],U[],V[]
 VAR U2V2=DOT(U,U) * DOT(V,V)
 VAR RP=U2V2+DOT(U,V)
 'IF RP<#EPSILON*U2V2 THEN
 IF RP<_EPSILON*U2V2 THEN
  'IF ABS(U[#X]>U[#Z]) THEN
  IF ABS(U[_X]>U[_Z]) THEN 
   'O[#X]=-U[#Y]:O[#Y]=U[#X]:O[#Z]=0:O[#W]=0
   O[_X]=-U[_Y]:O[_Y]=U[_X]:O[_Z]=0:O[_W]=0
 ELSE
   'O[#X]=0:O[#Y]=-U[#Z]:O[#Z]=U[#Y]:O[#W]=0
   O[_X]=0:O[_Y]=-U[_Z]:O[_Z]=U[_Y]:O[_W]=0
  ENDIF
 ELSE
  CROSS O,U,V
  'O[#W]=RP
  O[_W]=RP
 ENDIF
 NORMALIZE O,O
END

COMMON DEF QUAT_TO_AXISANGLE O[],Q[]
 'COPY O,0,Q,0,3:O[#W]=0
 COPY O,0,Q,0,3:O[_W]=0
 NORMALIZE O,O
 'O[#W]=2*ACOS(Q[#W])
 O[_W]=2*ACOS(Q[_W])
END

COMMON DEF QUAT_TO_MAT3 O[],Q[]
'VAR XX=Q[#X]*Q[#X]
 'VAR YY=Q[#Y]*Q[#Y]
 'VAR ZZ=Q[#Z]*Q[#Z]
 'VAR XY=Q[#X]*Q[#Y]
 'VAR XZ=Q[#X]*Q[#Z]
 'VAR YZ=Q[#Y]*Q[#Z]
 'VAR WX=Q[#W]*Q[#X]
 'VAR WY=Q[#W]*Q[#Y]
 'VAR WZ=Q[#W]*Q[#Z]
 VAR XX=Q[_X]*Q[_X]
 VAR YY=Q[_Y]*Q[_Y]
 VAR ZZ=Q[_Z]*Q[_Z]
 VAR XY=Q[_X]*Q[_Y]
 VAR XZ=Q[_X]*Q[_Z]
 VAR YZ=Q[_Y]*Q[_Z]
 VAR WX=Q[_W]*Q[_X]
 VAR WY=Q[_W]*Q[_Y]
 VAR WZ=Q[_W]*Q[_Z]
 O[0,0]=1-2*(YY+ZZ)
 O[0,1]=2*(XY+WZ)
 O[0,2]=2*(XZ-WY)
 O[1,0]=2*(XY-WZ)
 O[1,1]=1-2*(XX+ZZ)
 O[1,2]=2*(YZ+WX)
 O[2,0]=2*(XZ+WY)
 O[2,1]=2*(YZ-WX)
 O[2,2]=1-2*(XX+YY)
END

COMMON DEF MAT3_TO_QUAT O[],M[]
 VAR X=M[0,0]-M[1,1]-M[2,2]
 VAR Y=M[1,1]-M[0,0]-M[2,2]
 VAR Z=M[2,2]-M[0,0]-M[1,1]
 VAR W=M[0,0]+M[1,1]+M[2,2]
 VAR BIG=W
 VAR BIG%=3
 IF X>BIG THEN
  BIG=X:BIG%=0
 ENDIF
 IF Y>BIG THEN
  BIG=Y:BIG%=1
 ENDIF
 IF Z>BIG THEN
  BIG=Z:BIG%=2
 ENDIF
 BIG=SQR(BIG+1)*0.5
 VAR MUL=0.25/BIG
 X=MUL*(M[0,1]-M[1,0])
 Y=MUL*(M[1,2]-M[2,1])
 Z=MUL*(M[2,0]-M[0,2])
 IF 0==BIG% THEN
  O[_X]=BIG:O[_Y]=X:O[_Z]=Z:O[_W]=Y
 ELSEIF 1==BIG% THEN
  O[_X]=X:O[_Y]=BIG:O[_Z]=Y:O[_W]=Z
 ELSEIF 2==BIG% THEN
  O[_X]=Z:O[_Y]=Y:O[_Z]=BIG:O[_W]=X
 ELSE
  O[_X]=Y:O[_Y]=Z:O[_Z]=X:O[_W]=BIG
 ENDIF
END

COMMON DEF DISTSQ_POINT_LINE(O[],P[],A[],B[])
 VAR AB[LEN(A)]
 'ARYOP #AOPSUB,AB,B,A
 VAR I%
 FOR I%=0 TO LEN(AB)-1 AB[I%]=B[I%]-A[I%] NEXT
 VAR AP[LEN(A)]
 'ARYOP #AOPSUB,AP,P,A
 FOR I%=0 TO LEN(AP)-1 AP[I%]=P[I%]-A[I%] NEXT
 VAR AB2=DOT(AB,AB)
 VAR AP2=DOT(AP,AP)
 VAR ABAP=DOT(AB,AP)
 RETURN AP2-ABAP*ABAP/AB2
END

COMMON DEF CLOSEST_POINT_LINE(O[],P[],A[],B[])
 VAR AB[LEN(A)]
 'ARYOP #AOPSUB,AB,B,A
 VAR I%
 FOR I%=0 TO LEN(AB)-1 AB[I%]=B[I%]-A[I%] NEXT
 VAR AP[LEN(A)]
 'ARYOP #AOPSUB,AP,P,A
 FOR I%=0 TO LEN(AP)-1 AP[I%]=P[I%]-A[I%] NEXT
 VAR T=DOT(AP,AB)/DOT(AB,AB)
 'ARYOP #AOPMAD,O,AB,T,A
 FOR I%=0 TO LEN(O)-1 O[I%]=AB[I%]*T+A[I%] NEXT
 RETURN T
END

COMMON DEF CLOSEST_POINT_SEGMENT(O[],P[],A[],B[])
 VAR AB[LEN(A)]
 'ARYOP #AOPSUB,AB,B,A
 VAR I%
 FOR I%=0 TO LEN(AB)-1 AB[I%]=B[I%]-A[I%] NEXT
 VAR AP[LEN(A)]
 'ARYOP #AOPSUB,AP,P,A
 FOR I%=0 TO LEN(AP)-1 AP[I%]=P[I%]-A[I%] NEXT
 VAR T=DOT(AP,AB)
 IF T<0 THEN COPY O,A RETURN 0 ENDIF
 VAR AB2=DOT(AB,AB)
 IF T>=AB2 THEN COPY O,B RETURN 1 ENDIF
 T=T/AB2
 'ARYOP #AOPMAD,O,AB,T,A
 FOR I%=0 TO LEN(O)-1 O[I%]=AB[I%]*T+A[I%] NEXT
 RETURN T
END